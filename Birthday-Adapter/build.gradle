apply plugin: 'android'

dependencies {
    compile 'com.android.support:support-v4:18.0.+'
    compile fileTree(dir: 'libs', includes: ['*.jar'])
}

android {
    compileSdkVersion 17
    buildToolsVersion "17.0.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 17
    }

    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {
        println "Found sign properties in gradle.properties! Signing buildâ€¦"
        
        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    productFlavors {
        full {
            packageName "org.birthdayadapter"
            buildConfig """
public final static boolean FULL_VERSION = true;
public final static boolean GOOGLE_PLAY_VERSION = false;
"""
        }
        full_google {
            packageName "org.birthdayadapter"
            buildConfig """
public final static boolean FULL_VERSION = true;
public final static boolean GOOGLE_PLAY_VERSION = true;
"""
        }
        free {
            packageName "org.birthdayadapter.free"
            buildConfig """
public final static boolean FULL_VERSION = false;
public final static boolean GOOGLE_PLAY_VERSION = false;
"""
        }
    }
}